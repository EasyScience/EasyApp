import QtQuick

import EasyApplication.Gui.Style as EaStyle
import EasyApplication.Gui.Animations as EaAnimations
import EasyApplication.Gui.Elements as EaElements

EaElements.TextInput {
    id: control

    property string headerText: ""

    height: parent.height
    horizontalAlignment: Text.AlignHCenter
    verticalAlignment: Text.AlignVCenter
    hoverEnabled: true

    // ListView has its own row selection, so we don't need the TableView-style
    // "highlight the last-edited cell" behaviour — hence this separate component
    // with a color override. Track activeFocus (real keyboard focus) rather than
    // the per-FocusScope `focus` flag used in TextInput.qml, so sibling
    // ListViewDelegates don't stay blue after editing ends.
    color: enterFlash ?
               EaStyle.Colors.themeForeground :
               warned ?
                   EaStyle.Colors.red :
                   !enabled || readOnly || minored ?
                       EaStyle.Colors.themeForegroundMinor :
                       activeFocus || selected || hovered ?
                           EaStyle.Colors.themeForegroundHovered :
                           EaStyle.Colors.themeForeground
    Behavior on color { EaAnimations.ThemeChange {} }

    Keys.onEscapePressed: (event) => {
        focus = false
        event.accepted = true
    }

    onActiveFocusChanged: if (!activeFocus) cursorPosition = 0
    onTextChanged: if (!activeFocus) cursorPosition = 0
}
